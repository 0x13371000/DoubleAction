#define HDRTYPE HDR_TYPE_NONE
#include "common_ps_fxc.h"

sampler FBSampler        : register( s0 );
sampler VignetteSampler  : register( s1 );
sampler FilmGrainSampler : register( s2 );
sampler OverlaySampler   : register( s3 );

const float flSlowMoScale   : register( c0 );
const float flSlowMoFilter  : register( c1 );
const float flVignetteScale : register( c2 );
const float flNoiseScale    : register( c3 );
const float flBias          : register( c4 );
const float flGrainOffset   : register( c5 );

struct PS_INPUT
{
	float2 texCoord       : TEXCOORD0;
};

HALF4 main( PS_INPUT i ) : COLOR
{
	float4 aflFB = tex2D( FBSampler, i.texCoord );

	float flFBMax = max( aflFB.r, max( aflFB.g, aflFB.b ) );
	float flFBMin = min( aflFB.r, min( aflFB.g, aflFB.b ) );

	float flFBLevel = ( flFBMax + flFBMin ) / 2;
	float flFBSaturation = 0;
	if (flFBMax != flFBMin)
	{
		if (flFBLevel < 0.5)
			flFBSaturation = (flFBMax - flFBMin)/(flFBMax + flFBMin);
		else
			flFBSaturation = (flFBMax - flFBMin)/(2 - flFBMax - flFBMin);
	}

	// Only filter the desaturated and dark areas.
	float flSaturationRamp = RemapValClamped(flFBSaturation, 0, 0.4, 1, 0);
	float flLevelRamp = RemapValClamped(flFBLevel, 0, 0.4, 1, 0);

	float flTotalRamp = max(flSaturationRamp, flLevelRamp);

	float4 aflOutput = aflFB;

	aflOutput.rg = aflOutput.rg - flSlowMoFilter * flTotalRamp;

	float4 aflVignette = tex2D( VignetteSampler, i.texCoord );
	aflOutput.rgb = lerp(aflOutput.rgb, aflVignette.rgb, aflVignette.a * flVignetteScale);

	// Adjust levels. Crush the blacks and blow out the whites.
	float flExponent = log( flBias ) * -1.4427f;
	aflOutput.r = pow(RemapValClamped(aflOutput.r, 0, .56, 0, 1), flExponent);
	aflOutput.g = pow(RemapValClamped(aflOutput.g, 0, .56, 0, 1), flExponent);
	aflOutput.b = pow(RemapValClamped(aflOutput.b, 0, .56, 0, 1), flExponent);

	float flFBGrey = ( aflOutput.r + aflOutput.g + aflOutput.b ) / 3;

	aflOutput.rgb = lerp(aflOutput.rgb, float3(flFBGrey, flFBGrey, flFBGrey), .36);

	float4 aflGrain = tex2D( FilmGrainSampler, i.texCoord + float2(flGrainOffset, flGrainOffset) );

	// Use a "screen" filter to apply grain to output. This helps bring the general light levels up a bit.
	aflGrain.rgb = 1 - (1 - aflOutput.rgb)*(1 - aflGrain.rgb);

	// Scale it by the noise scale.
	aflOutput.rgb = lerp(aflOutput.rgb, aflGrain, flNoiseScale);

	float4 aflOverlay = tex2D( OverlaySampler, i.texCoord );
	float4 aflConditional = step(0.5, aflOutput);
	float4 aflOverlaid = lerp(aflOutput*aflOverlay*2, 1 - (2*(1-aflOutput)*(1-aflOverlay)), aflConditional);

	// Scale it by the overlay scale.
	aflOutput.rgb = lerp(aflOutput.rgb, aflOverlaid.rgb, 0.15);

	return FinalOutput( lerp(aflFB, aflOutput, flSlowMoScale), 0, PIXEL_FOG_TYPE_NONE, TONEMAP_SCALE_NONE );
}
